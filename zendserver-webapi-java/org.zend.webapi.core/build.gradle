apply plugin: 'java'
apply plugin: 'eclipse'
apply from:"file:emma.gradle"

sourceCompatibility = 1.5
version = '0.0.15'
srcFolder = 'src'
testFolder = 'test'

repositories {
	mavenCentral()
	maven {
		url "http://maven.restlet.org"
	}
}

sourceSets {
	main {
		java {
			srcDir srcFolder
		}
	}
	test {
		java {
			srcDir testFolder
		}
	}
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.+'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.5'
	compile group: 'org.restlet.jse', name: 'org.restlet', version: '2.0.4'
	compile group: 'org.restlet.jse', name: 'org.restlet.ext.xml', version: '2.0.4'
}

test {
	scanForTestClasses = false
	include '**/*AllTests.class'
}

compileJava.doFirst {
	copy {
		from testFolder
		into sourceSets.test.output.classesDir
		exclude '**/*.java'
		exclude '**/.svnignore'
	}
}

task createTestJar(type: Jar) {
	archiveName = project.rootProject.name + '.test-' + version + '.jar'
	from sourceSets.test.output
	from testFolder
	exclude '**/*.java'
	exclude '**/.svnignore'
	exclude '**/.svn'
}

task createBinPackage(type: Zip) {
	archiveName = project.rootProject.name + '-' + version + '-bin.zip'
	into('lib') {
		from project.libsDir
		from configurations.compile
	}
	into('examples') {
		from 'examples'
	}
	into('.') {
		from project.rootDir
		include 'copyright.txt'
		include 'license.html'
		include 'readme.txt'
	}
}

task createSrcPackage(type: Zip) {
	into('lib') {
		from project.libsDir
		from configurations.compile
	}
	into('src') {
		from srcFolder
	}
	into('test') {
		from testFolder
	}
	into('junit') {
		from project.testReportDir
		from project.testResultsDir
	}
	into('docs') {
		from project.docsDir
	}
	into('examples') {
		from 'examples'
	}
	into('.') {
		from project.rootDir
		include 'copyright.txt'
		include 'license.html'
		include 'readme.txt'
	}
}

uploadArchives {
	repositories {
	   flatDir {
		   dirs 'file:///Users/galAnonim/temp'
	   }
	}
}